{
  config,
  lib,
  opts,
  ...
}:
let
  themes = opts.themes or [ ];
  hasThemes = themes != [ ];
  desktop = opts.desktop or "";
  hasDesktop = desktop != "";
in
{
  programs.nixvim = {
    opts = {
      termguicolors = true;

      ignorecase = true;
      smartcase = true;
      hlsearch = true;
      incsearch = true;

      wildmenu = true;
      wildmode = "longest:full,full";
      wildignorecase = true;

      number = true;
      relativenumber = true;

      splitright = true;
      splitbelow = true;

      diffopt = [
        "internal"
        "filler"
        "closeoff"
        "indent-heuristic"
        "linematch:60"
      ];

      foldlevel = 99;
    };

    diagnostic = {
      settings = {
        virtual_text = false;
      };
    };

    clipboard = {
      register = "unnamedplus";
      providers = lib.mkIf hasDesktop {
        wl-copy.enable = true;
        xclip.enable = true;
      };
    };
    # Use OSC 52 for clipboard sync when connected via ssh
    extraConfigLua = ''
      local function is_ssh()
      	return vim.env.SSH_CONNECTION or vim.env.SSH_CLIENT or vim.env.SSH_TTY
      end

      if is_ssh() then
      	local function paste()
      		return {
      			vim.fn.split(vim.fn.getreg(""), "\n"),
      			vim.fn.getregtype(""),
      		}
      	end
      	vim.g.clipboard = {
      		name = "OSC 52",
      		copy = {
      			["+"] = require("vim.ui.clipboard.osc52").copy("+"),
      			["*"] = require("vim.ui.clipboard.osc52").copy("*"),
      		},
      		paste = {
      			["+"] = paste,
      			["*"] = paste,
      		},
      	}
      end
    ''
    # Auto reload stylix colorscheme
    + lib.optionalString hasThemes ''
      local function reload_theme()
      	vim.defer_fn(function()
      		local f = io.open(vim.env.MYVIMRC, "r")
      		if f then
      			local c = f:read("*all")
      			f:close()

      			-- 1. Reload the base16 colorscheme generated by Stylix/mini.base16
      			local b16 = c:match('require%("mini.base16"%)%.setup%({.-}%)')
      			if b16 then
      				local func, err = loadstring(b16)
      				if func then
      					func()
      				end
      			end

      			-- 2. Reload the "Highlight groups" block generated by NixVim/Home Manager
      			local hl_block = c:match("%-%- Highlight groups {{%s*(.-)%s*%-%- }}")
      			if hl_block then
      				local func, err = loadstring(hl_block)
      				if func then
      					func()
      				else
      					vim.notify("Highlight block parse error: " .. tostring(err), vim.log.levels.ERROR)
      				end
      			end

      			-- 3. Force transparency fallback
      			local fallback_h = { "Normal", "LineNr", "SignColumn", "NonText", "NormalFloat", "FloatBorder" }
      			for _, n in ipairs(fallback_h) do
      				vim.api.nvim_set_hl(0, n, { bg = "none" })
      			end

      			vim.notify("Theme & Highlights reloaded")
      		end
      	end, 100)
      end

      -- Initialize filesystem event watcher to monitor Neovim config directory
      local w = vim.loop.new_fs_event()
      local config_dir = vim.fn.expand("${config.xdg.configHome}/nvim")

      w:start(
      	config_dir,
      	{},
      	vim.schedule_wrap(function(err, fname, events)
      		if err then
      			return
      		end
      		-- Trigger reload only when init.lua is updated
      		if fname == "init.lua" then
      			reload_theme()
      		end
      	end)
      )
    '';
  };
}
